ad_page_contract {
  This is the main page for the package.  It displays all entries
  provides links to create, edit and delete these

  @author Gustaf Neumann (gustaf.neumann@wu-wien.ac.at)
  @creation-date Oct 23, 2005
  @cvs-id $Id$

  @param object_type show objects of this class and its subclasses
} -query {
  object_type:optional
  folder_id:optional
}

if {![info exists object_type]} {
  set object_type ::xowiki::Page
}
if {![info exists folder_id]} {
  set folder_id [$object_type require_folder -name xowiki]
}

set path [ns_urldecode [ad_conn path_info]]
#ns_log notice "-- path=<$path>"
if {$path ne ""} {
  set item_id [::Generic::CrItem lookup -name $path -parent_id $folder_id]

  if {$item_id == 0} {
    if {[regexp {^(..)/(.*)$} $path _ lang name]} {
      set name $lang:$name
    } elseif {![regexp {^..:} $path]} {
      set name [string range [lang::conn::locale] 0 1]:$path 
    }
    if {[info exists name]} {
      set item_id [::Generic::CrItem lookup -name $name -parent_id $folder_id]
    }
    if {$item_id == 0} {
      set subst_blank_in_name [$folder_id get_payload subst_blank_in_name]
      if {$subst_blank_in_name == 1} {
	regsub -all { } $name "_" name
	set item_id [::Generic::CrItem lookup -name $name -parent_id $folder_id]
      }
    }
  }
  #ns_log notice "-- path=<$path> item_id=$item_id"
  if {$item_id != 0} {
    if {[ns_queryget item_id] eq ""} {rp_form_put item_id $item_id}
    if {[ns_queryget folder_id] eq ""} {rp_form_put folder_id $folder_id}
    rp_internal_redirect "/packages/xowiki/www/view"
    ad_script_abort
  }
}
